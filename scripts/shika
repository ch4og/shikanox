#!/usr/bin/env -S guix shell guile -- guile --no-auto-compile
;; -*- mode: scheme -*-
;; SPDX-FileCopyrightText: 2025 Nikita Mitasov <me@ch4og.com>
;; SPDX-License-Identifier: GPL-3.0-or-later
!#

(define repo-root
  (dirname (dirname (canonicalize-path (current-filename)))))

(add-to-load-path repo-root)

(use-modules (ice-9 match)
             (ice-9 ftw)
             (shika lib channels)
             (shika lib substitutes))

(define (run-guix kind extra-args)
  (let ((sudo (if (string=? kind "system") "sudo" "")))
    (display (string-append "Running " kind " reconfigure...\n"))
    (system (string-join (append (list "cd"
                                       repo-root
                                       "&&"
                                       "exec"
                                       sudo
                                       "guix"
                                       "time-machine"
                                       "-C"
                                       (string-append repo-root "/shika/lib/channels.scm")
                                       "--"
                                       kind
                                       "reconfigure"
                                       (string-append repo-root "/shika/" kind "/config.scm")
                                       shika-subs-urls
                                       "-L"
                                       repo-root) extra-args) " "))))

(define (run-refresh extra-args)
  (for-each
   (lambda (name)
     (format #t "Refreshing ~a~%" name)
     (system (string-join (append (list "cd"
                                        repo-root
                                        "&&"
                                        "guix"
                                        "refresh"
                                        "-L"
                                        repo-root
                                        "-u"
                                        "--select="
                                        "'module:(shika packages " name ")'")
                                  extra-args) " ")))
   (map
    (lambda (f) (substring f 0 (- (string-length f) 4)))
    (filter
     (lambda (f) (string-suffix? ".scm" f))
     (scandir (string-append repo-root "/shika/packages"))))))

(define (usage command)
  (display (string-append "Usage: " command " COMMAND [args...]\n\n"))
  (display "Commands:\n")
  (display "  system   [args...]  - reconfigure system\n")
  (display "  home     [args...]  - reconfigure home\n")
  (display "  refresh  [args...]  - refresh channel packages\n"))

(define (selection cmd)
  (match (cdr cmd)
    (("system" . rest) (run-guix "system" rest))
    (("home" . rest) (run-guix "home" rest))
    (("refresh" . rest) (run-refresh rest))
    (_ (usage (car cmd)) (exit 1))))

(selection (command-line))
