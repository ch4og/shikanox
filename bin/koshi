#!/usr/bin/env -S guix shell guile -- guile --no-auto-compile
;; -*- mode: scheme -*-
;; SPDX-FileCopyrightText: 2025 Nikita Mitasov <me@ch4og.com>
;; SPDX-License-Identifier: GPL-3.0-or-later
!#

(define repo-guix-root
  (string-append (dirname (dirname (canonicalize-path (current-filename)))) "/guix"))

(add-to-load-path repo-guix-root)

(use-modules (ice-9 match)
             (ice-9 ftw))

(define (run-refresh extra-args)
  (for-each
   (lambda (name)
     (format #t "Refreshing ~a~%" name)
     (system (string-join (append (list "cd"
                                        repo-guix-root
                                        "&&"
                                        "guix"
                                        "refresh"
                                        "-L"
                                        repo-guix-root
                                        "-u"
                                        (string-join `("--select='module:(koshi packages " ,name ")'") ""))
                                  extra-args) " ")))
   (map
    (lambda (f) (substring f 0 (- (string-length f) 4)))
    (filter
     (lambda (f) (string-suffix? ".scm" f))
     (scandir (string-append repo-guix-root "/koshi/packages"))))))

(define (run-format)
  (system "find guix/koshi -type f -name \"*.scm\" -exec emacs --batch {} --eval '(progn (indent-region (point-min) (point-max)) (save-buffer))' \\; ;"))

(define (usage command)
  (display (string-append "Usage: " command " COMMAND [args...]\n\n"))
  (display "Commands:\n")
  (display "  update  [args...]   - refresh channel packages\n")
  (display "  format              - format code with Emacs\n"))

(define (selection cmd)
  (match (cdr cmd)
    (("update" . rest) (run-refresh rest))
    (("format") (run-format))
    (_ (usage (car cmd)) (exit 1))))

(selection (command-line))
