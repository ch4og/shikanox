#!/usr/bin/env -S guix shell guile -- guile --no-auto-compile
;; -*- mode: scheme -*-
;; SPDX-FileCopyrightText: 2025 Nikita Mitasov <me@ch4og.com>
;; SPDX-License-Identifier: GPL-3.0-or-later
!#

(define repo-guix-root
  (string-append (dirname (dirname (canonicalize-path (current-filename)))) "/guix"))

(add-to-load-path repo-guix-root)

(use-modules (ice-9 match)
             (ice-9 ftw)
             (shika lib channels)
             (shika lib substitutes))

(define (run-guix kind extra-args)
  (let ((sudo (if (string=? kind "system") "sudo" "")))
    (display (string-append "Running " kind " reconfigure...\n"))
    (system (string-join (append (list "cd"
                                       repo-guix-root
                                       "&&"
                                       "exec"
                                       sudo
                                       "guix"
                                       "time-machine"
                                       "-C"
                                       (string-append repo-guix-root "/shika/lib/channels.scm")
                                       "--"
                                       kind
                                       "reconfigure"
                                       (string-append repo-guix-root "/shika/" kind "/config.scm")
                                       %shika-subs-urls
                                       "-L"
                                       repo-guix-root) extra-args) " "))))

(define (run-format)
  (system "find guix/shika -type f -name \"*.scm\" -exec emacs --batch {} --eval '(progn (indent-region (point-min) (point-max)) (save-buffer))' \\; ;"))

(define (run-update)
  (system "sh bin/update-channels"))

(define (usage command)
  (display (string-append "Usage: " command " COMMAND [args...]\n\n"))
  (display "Commands:\n")
  (display "  system   [args...]  - reconfigure system\n")
  (display "  home     [args...]  - reconfigure home\n")
  (display "  update              - update channels\n")
  (display "  format              - format code with Emacs\n"))

(define (selection cmd)
  (match (cdr cmd)
    (("system" . rest) (run-guix "system" rest))
    (("home" . rest) (run-guix "home" rest))
    (("update") (run-update))
    (("format") (run-format))
    (_ (usage (car cmd)) (exit 1))))

(selection (command-line))
